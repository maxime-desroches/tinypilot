Import('env', 'envCython', 'arch', 'common')

gen = "c_generated_code"

casadi_model = [
  f'{gen}/long_model/long_expl_ode_fun.c',
  f'{gen}/long_model/long_expl_vde_forw.c',
]

casadi_cost_y = [
  f'{gen}/long_cost/long_cost_y_fun.c',
  f'{gen}/long_cost/long_cost_y_fun_jac_ut_xt.c',
  f'{gen}/long_cost/long_cost_y_hess.c',
]

casadi_cost_e = [
  f'{gen}/long_cost/long_cost_y_e_fun.c',
  f'{gen}/long_cost/long_cost_y_e_fun_jac_ut_xt.c',
  f'{gen}/long_cost/long_cost_y_e_hess.c',
]

casadi_cost_0 = [
  f'{gen}/long_cost/long_cost_y_0_fun.c',
  f'{gen}/long_cost/long_cost_y_0_fun_jac_ut_xt.c',
  f'{gen}/long_cost/long_cost_y_0_hess.c',
]

casadi_constraints = [
  f'{gen}/long_constraints/long_constr_h_fun.c',
  f'{gen}/long_constraints/long_constr_h_fun_jac_uxt_zt.c',
  f'{gen}/long_constraints/long_constr_h_e_fun.c',
  f'{gen}/long_constraints/long_constr_h_e_fun_jac_uxt_zt.c',
]

build_files = [f'{gen}/acados_solver_long.c'] + casadi_model + casadi_cost_y + casadi_cost_e +  \
              casadi_cost_0 + casadi_constraints

# extra generated files used to trigger a rebuild
generated_files = [
  f'{gen}/Makefile',

  f'{gen}/main_long.c',
  f'{gen}/acados_solver_long.h',

  f'{gen}/long_model/long_expl_vde_adj.c',

  f'{gen}/long_model/long_model.h',
  f'{gen}/long_constraints/long_h_constraint.h',
  f'{gen}/long_constraints/long_h_e_constraint.h',
  f'{gen}/long_cost/long_cost_y_fun.h',
  f'{gen}/long_cost/long_cost_y_e_fun.h',
  f'{gen}/long_cost/long_cost_y_0_fun.h',
] + build_files

lenv = env.Clone()
lenv.Clean(generated_files, Dir(gen))

lenv.Command(generated_files,
             ["long_mpc.py"],
             f"cd {Dir('.').abspath} && python long_mpc.py")

lenv["CFLAGS"].append("-DACADOS_WITH_QPOASES")
lenv["CXXFLAGS"].append("-DACADOS_WITH_QPOASES")
lenv["CCFLAGS"].append("-Wno-unused")
lenv["LINKFLAGS"].append("-Wl,--disable-new-dtags")
lib_solver = lenv.SharedLibrary(f"{gen}/acados_ocp_solver_long",
                                build_files,
                                LIBS=['m', 'acados', 'hpipm', 'blasfeo', 'qpOASES_e'])

# generate cython stuff
acados_ocp_solver_pyx = File("#pyextra/acados_template/acados_ocp_solver_pyx.pyx")
libacados_ocp_solver_long = File('#selfdrive/controls/lib/longitudinal_mpc_lib/c_generated_code/libacados_ocp_solver_long.so')
libacados_ocp_solver_long_pxd = File('#selfdrive/controls/lib/longitudinal_mpc_lib/c_generated_code/acados_solver.pxd')
libacados_ocp_solver_long_c = File('#selfdrive/controls/lib/longitudinal_mpc_lib/c_generated_code/acados_ocp_solver_pyx.c')

lenv2 = envCython.Clone()
lenv2["LINKFLAGS"] += [libacados_ocp_solver_long.get_labspath()]
lenv2.Command(libacados_ocp_solver_long_c,
  [acados_ocp_solver_pyx, libacados_ocp_solver_long_pxd],  # TODO fix
  f'cython' + \
  f' -o {libacados_ocp_solver_long_c.get_labspath()}' + \
  f' -I {libacados_ocp_solver_long_pxd.get_dir().get_labspath()}' + \
  f' -I {acados_ocp_solver_pyx.get_dir().get_labspath()}' + \
  f' {acados_ocp_solver_pyx.get_labspath()}')
lib_cython = lenv2.Program(f'{libacados_ocp_solver_long_c.get_dir().get_labspath()}/acados_ocp_solver_pyx.so',
  [libacados_ocp_solver_long_c])
lenv2.Depends(lib_cython, lib_solver)
