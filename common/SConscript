Import('env', 'envCython', 'target')

# *** common
common_libs = [
  'params.cc',
  'statlog.cc',
  'swaglog.cc',
  'util.cc',
  'i2c.cc',
  'watchdog.cc',
  'ratekeeper.cc'
]
if target != "Darwin":
  common_libs.append('gpio.cc')

common = env.Library('common', common_libs, LIBS="json11")

# *** gpucommon
gpucommon_files = [
  'clutil.cc',
]
gpucommon = env.Library('gpucommon', gpucommon_files)

# *** tests
if GetOption('extras'):
  test_files = ['tests/test_runner.cc', 'tests/test_util.cc', 'tests/test_swaglog.cc', 'tests/test_ratekeeper.cc']
  env.Program('tests/test_common', test_files, LIBS=[common, 'json11', 'zmq', 'pthread'])

# *** cython bindings
params_python = envCython.Program('params_pyx.so', 'params_pyx.pyx', LIBS=envCython['LIBS'] + [common, 'zmq', 'json11'])

SConscript([
  'kalman/SConscript',
  'transformations/SConscript'  
])

Import('simple_kalman_python', 'transformations_python')
common_python = [params_python, simple_kalman_python, transformations_python]

Export('common', 'gpucommon', 'common_python')
