Import('env', 'envCython', 'target')

# *** common
common_libs = [
  'params.cc',
  'statlog.cc',
  'swaglog.cc',
  'util.cc',
  'i2c.cc',
  'watchdog.cc',
]
if target != "Darwin":
  common_libs.append('gpio.cc')

common = env.Library('common', common_libs, LIBS="json11")

# *** gpucommon
gpucommon_files = [
  'clutil.cc',
]
gpucommon = env.Library('gpucommon', gpucommon_files)

# *** tests
if GetOption('test'):
  env.Program('tests/test_util', ['tests/test_util.cc'], LIBS=[common])
  env.Program('tests/test_swaglog', ['tests/test_swaglog.cc'], LIBS=[common, 'json11', 'zmq', 'pthread'])

# *** params & clock
envCython.Program('clock.so', 'clock.pyx')
envCython.Program('params_pyx.so', 'params_pyx.pyx', LIBS=envCython['LIBS'] + [common, 'zmq', 'json11'])

Export('common', 'gpucommon')
