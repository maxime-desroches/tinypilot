Import('env', 'envCython', 'arch')

templates = Glob('#rednose/templates/*')
sympy_helpers = "#rednose/helpers/sympy_helpers.py"
ekf_sym = "#rednose/helpers/ekf_sym.py"
ekf_sym_pyx = "#rednose/helpers/ekf_sym_pyx.pyx"
ekf_sym_cc = "#rednose/helpers/ekf_sym.cc"
common_ekf = "#rednose/helpers/common_ekf.cc"

to_build = {
    'live': ('live_kf.py', 'generated', True),
    'car': ('car_kf.py', 'generated', True),
}

if arch != "aarch64":
    to_build.update({
        'gnss': ('gnss_kf.py', 'generated', False),
        'loc_4': ('loc_kf.py', 'generated', False),
        'pos_computer_4': ('#rednose/helpers/lst_sq_computer.py', 'generated', False),
        'pos_computer_5': ('#rednose/helpers/lst_sq_computer.py', 'generated', False),
        'feature_handler_5': ('#rednose/helpers/feature_handler.py', 'generated', False),
        'lane': ('#xx/pipeline/lib/ekf/lane_kf.py', 'generated', False),
    })

found = {}

for target, (command, generated_folder, use_cpp) in to_build.items():
    if File(command).exists():
        found[target] = (command, generated_folder, use_cpp)

lib_target = [common_ekf]
for target, (command, generated_folder, use_cpp) in found.items():
    target_files = File([f'{generated_folder}/{target}.cpp', f'{generated_folder}/{target}.h'])
    command_file = File(command)

    env.Command(target_files,
                [command_file, templates, sympy_helpers, ekf_sym],
                command_file.get_abspath() + " " + target + " " + Dir(generated_folder).get_abspath())

    if use_cpp:
        lib_target.append(target_files[0])
    else:
        env.SharedLibrary(f'{generated_folder}/' + target, target_files[0])

libkf = env.SharedLibrary(f'{generated_folder}/libkf', lib_target)

lenv = envCython.Clone()
lenv["LINKFLAGS"] += [libkf[0].get_labspath()]
ekf_sym_so = lenv.Program('#rednose/helpers/ekf_sym_pyx.so', [ekf_sym_pyx, ekf_sym_cc, common_ekf],
                          LIBPATH=[generated_folder])
lenv.Depends(ekf_sym_so, libkf)