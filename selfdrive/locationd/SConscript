Import('env', 'common', 'cereal', 'messaging', 'libkf')

loc_libs = [cereal, messaging, 'zmq', common, 'capnp', 'kj', 'kaitai', 'pthread']

if GetOption('kaitai'):
  generated = Dir('generated').srcnode().abspath
  cmd = f"kaitai-struct-compiler --target cpp_stl --outdir {generated} $SOURCES"
  env.Command(['generated/ubx.cpp', 'generated/ubx.h'], 'ubx.ksy', cmd)
  env.Command(['generated/gps.cpp', 'generated/gps.h'], 'gps.ksy', cmd)

env.Program("ubloxd", ["ubloxd.cc", "ublox_msg.cc", "generated/ubx.cpp", "generated/gps.cpp"], LIBS=loc_libs)

lenv = env.Clone()
ekf_sym_cc = env.Object("#rednose/helpers/ekf_sym.cc")
lenv["LIBPATH"] += [libkf[0].get_dir().get_abspath()]
locationd = lenv.Program("locationd", ["locationd.cc", "models/live_kf.cc", ekf_sym_cc], LIBS=loc_libs + [libkf])
lenv.Depends(locationd, libkf)
